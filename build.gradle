// Top-level build file where you can add configuration options common to all sub-projects/modules.
gradle.addProjectEvaluationListener(new ProjectEvaluationListener() {
    @Override
    void beforeEvaluate(Project project) {
        if (project.name.contains("plugin")) return
        if (project.name.endsWith("app")) {
            project.apply {
                from "${project.rootDir.path}/buildApp.gradle"
            }
        } else if (project.name.endsWith("java")) {

            project.apply {
                from "${project.rootDir.path}/buildJava.gradle"
            }
        } else {
            project.apply {
                from "${project.rootDir.path}/buildLib.gradle"
            }
        }
    }

    @Override
    void afterEvaluate(Project project, ProjectState projectState) {

    }
})
buildscript {
    ext.enKtx = true
    ext.enX = true
    ext.enDatabinding = true
    ext.enLog = true
    apply from: "${rootDir.path}/config.gradle"
    repositories {
        addRepos(repositories)
    }
    dependencies {
        def classpathDeps = cfgNdClasspath()
        if (classpathDeps && classpathDeps.size() > 0) {
            classpathDeps.forEach {
                if (it && it.startsWithAny("classpath__")) {
                    classpath it.replaceAll("classpath__", "")
                }
            }
        }
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        addRepos(repositories)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}